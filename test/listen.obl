#!/home/jan/Projects/obelix/install/bin/obelix

import net

func srv()
  self.socket = net.server(12345)
  self.lock = mutex()
  print("Server socket created")

  func listen()
    self.socket.listen(self)
  end

  func __call__(client)
    print("${0} connected", client.host)
    carry_on = true
    while carry_on
      cmd = client.fd.readline()
      print("Command: ${0}", cmd)
      if cmd == "HELLO"
        client.fd.print("Hello, ${0}.", client.host)
      elif cmd.startswith("ECHO")
        client.fd.print(cmd.slice(5, 0))
      elif cmd == "LOCK"
        self.lock.lock()
        client.fd.print("Lock obtained")
      elif cmd == "UNLOCK"
        self.lock.unlock()
        client.fd.print("Lock released")
      elif cmd == "BYE"
        client.fd.print("Bye to you to!")
        carry_on = false
      elif cmd == "SHUTDOWN"
        client.fd.print("Farewell, cruel world...")        
        self.socket.interrupt()
      else
        client.fd.print("Sorry, I don't understand ${0}", cmd)
      end
    end
    return 0
  end
end

s = new srv()
s.listen()
