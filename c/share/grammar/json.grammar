%
  grammar_buildfunc:   "json_grammar_build"
  lexercfg_buildfunc:  "json_lexercfg_build"
  prefix:              "json_parse_"
  lexer:               "number"
  lexer:               "qstring: quotes=\""
  lexer:               "whitespace: ignoreall=1"
%

document                    := value             [ get_value                 ]
                             ;

array                       := '['               [ bookmark                  ]
                               value *,','
                               ']'               [ rollup_list               ]
                             ;

object                      := '{'               [ bookmark                  ]
                               key_value_pair *,','
                               '}'               [ rollup_list to_dictionary ]
                             ;

key_value_pair              := '\"'              [ push                      ]
                               ':'
                               value             [ rollup_nvp                ]
                             ;

value                       := '\"'              [ push                      ]
                             | 'd'               [ push                      ]
                             | 'x'               [ push                      ]
                             | 'f'               [ push                      ]
                             | array
                             | object
                             | "null"            [ push_const: "ptr:null"    ]
                             | "true"            [ push_const: "bool:1"      ]
                             | "false"           [ push_const: "bool:0"      ]
                             ;
