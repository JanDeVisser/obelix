%
  grammar_buildfunc:   "uri_grammar_build"
  lexercfg_buildfunc:  "uri_lexercfg_build"
  prefix:              "uri_parse_"
  lexer:               "identifier: filter=X9+.%~-_;startwith=;tokencode=u"
%

uri                         :=                   [ init                      ]
                               scheme
                               ':'
                               authority_or_path ?
                               query ?
                               fragment ?        [ done                      ]
                             ;

scheme                      := 'u'               [ set_scheme                ]
                             ;

authority_or_path           := authority_or_absolute_path
                             | relative_path
                             ;

authority_or_absolute_path  := '/' _authority_or_absolute_path
                             ;

_authority_or_absolute_path := authority_path
                             | absolute_path
                             ;

authority_path              := '/'
                               authority ?
                               '/' ?
                               relative_path ?
                             ;

authority                   := 'u'               [ push_tokenstring          ]
                               authentication_or_host
                             ;

authentication_or_host      := authentication_or_port
                             |                   [ set_host                  ]
                             ;

authentication_or_port      :=
                               ':'
                               'u'               [ push_tokenstring          ]
                               at_or_done
                             ;

at_or_done                  := at
                             |                   [ set_port set_host         ]
                             ;

at                          := '@'               [ set_credentials          ]
                               'u'               [ push_tokenstring set_host ]
                               port ?
                             ;

port                        := ':' 'u'           [ push_tokenstring set_port ]
                             ;

absolute_path               :=                   [ bookmark pushval: "/"     ]
                               'u'               [ push                      ]
                               pathcomponent *   [ rollup_name set_path      ]
                             ;
relative_path               := 'u'               [ bookmark push             ]
                               pathcomponent *   [ rollup_name set_path      ]
                             ;

pathcomponent               := '/' 'u'           [ push                      ]
                             ;

query                       := '?'               [ bookmark                  ]
                               queryparams ?     [ rollup_list set_query     ]
                             ;

queryparams                 := queryparam
                               _queryparams *
                             ;

_queryparams                := '&' queryparam
                            ;

queryparam                  := 'u'               [ push_tokenstring          ]
                               '='
                               'u'             [ push_tokenstring rollup_nvp ]
                             ;

fragment                    := '#' 'u'           [ set_fragment              ]
                             ;
