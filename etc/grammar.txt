%
name: simple_expr
prefix: script_parse_
init: init
ignore_ws: true
hashpling: true
%

program                     := statements 
                             ;

statements                  := statement
                               statements 
                             | 
                             ;

statement                   := conditional
                             | while_loop
                             | for_loop
                             | func_def
                             | return_stmt
                             | identifier
                               assignment_or_call
                             ;

conditional                 := "if"
                               expr
                               "then"         [ emit_test       ]
                               statements
                               else_clause
                               end_stmt       [ emit_end        ]
                             ;

else_clause                 := "else"         [ emit_else       ]
                               statements
                             |
                             ;
                             
end_stmt                    := "end"
                             ;

while_loop                  := "while"        [ push_label      ]
                               expr           [ emit_test       ]
                               statements
                               end_stmt       [ emit_end_while  ]
                             ;

for_loop                    := "for"
                               'i'
                               "in"
                               expr
                               statements
                               end_stmt
                             ;
                             
func_def                    := "func"
                               'i'            [ push_last_token  ]
                               (              [ init_param_count ]
                               parlist
                               )              [ start_function   ]
                               statements
                               end_stmt       [ end_function     ]
                             ;
                             
parlist                     := param
                               parlist_tail
                             |
                             ;

param                       := 'i'            [ push_param     ] 
                             ;

parlist_tail                := ,
                               parlist
                             |
                             ;

return_stmt                 := "return"
                               expr 
                             ;

assignment_or_call          := assignment
                             | func_call       [ emit_pop       ]
                             ;

assignment                  := =
                               expr            [ emit_assign     ]
                             ;

expr                        := predicate
                               predicatetail
                             ;
                         
predicatetail               := logic_op        [ push_last_token ]
                               predicate       [ emit_mathop     ]
                             |
                             ;
                               
predicate                   := term
                               termtail
                             ;

termtail                    := add_op          [ push_last_token ]
                               term            [ emit_mathop     ]
                               termtail
                             | 
                             ;

term                        := factor factortail 
                             ;

factortail                  := mult_op         [ push_last_token ]
                               factor          [ emit_mathop     ]
                               factortail
                             | 
                             ;

factor                      := ( expr )
                             | var_or_call 
                             | constant          [ emit_pushval              ]  
                             ;

var_or_call:                 := identifier       
                                call_or_empty
                              ;
                              
call_or_empty:              := func_call
                             |                   [ emit_pushvar              ]
                             ;

identifier                  := 'i'               [ init_identifier           ]
			       identifier_tail

identifier_tail             := .
                               identifier        [ push_identifier_comp      ]
                             |
                             ;
                             
constant                    := '"'
                             | '\''
                             | 'd'
                             | 'f'
                             | 'x'
                             ;

logic_op                    := > | ">=" | < | "<=" | "==" | "!=" ;
                             
add_op                      := + | - ;

mult_op                     := * | / | ^ | % ;

func_call                   := (           [ init_param_count ]
                               arglist
                               )           [ emit_func_call  ]
                             ;

arglist                     := argument
                               arglist_tail
                             ;

argument                    := expr        [ param_count     ] 
                             ;

arglist_tail                := ,
                               arglist
                             |
                             ;
 
