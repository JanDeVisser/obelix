%
prefix: script_parse_
init: "init"
done: "done"
ignore: whitespace
hashpling: true
%

program                     := statements 
                             ;

statements                  := statement
                               statements 
                             | 
                             ;

statement                   := conditional
                             | while_loop
                             | for_loop
                             | func_def
                             | return_stmt
                             | import_stmt
                             | new
                             |                   [ push: 0                   ] 
                               identifier
                               assignment_or_call
                             ;

conditional                 := "if"
                               expr
                               "then"            [ emit_test                 ]
                               statements
                               else_clause
                               end_stmt          [ emit_end                  ]
                             ;

else_clause                 := "else"            [ emit_else                 ]
                               statements
                             |
                             ;
                             
end_stmt                    := "end"
                             ;

while_loop                  := "while"           [ push_label                ]
                               expr              [ emit_test                 ]
                               statements
                               end_stmt          [ emit_end_while            ]
                             ;

for_loop                    := "for"
                               'i'
                               "in"
                               expr
                               statements
                               end_stmt
                             ;
                             
func_def                    := "func"
                               'i'               [ push:   $                 ]
                               (                 [ push:   0                 ]
                               parlist_or_void
                               )
                               func_block
                             ;
                             
func_block                  := "begin"           [ start_function            ]
                               statements
                               end_stmt          [ end_function              ]
                             | link_clause       [ native_function           ]
                             ;
                             
link_clause                 := "->" '"'
                             ;
                             
dummy                       :=
                             ;

parlist_or_void             := parlist
                             |
                             ;
                             
parlist                     := param
                               parlist_tail
                             ;

param                       := 'i'               [ push++:   $               ] 
                             ;

parlist_tail                := ,
                               parlist
                             |
                             ;

return_stmt                 := "return"          [ push: "END"               ]
                               expr              [ jump                      ]
                             ;

import_stmt                 := "import"          [ push: 0                   ]
                               identifier        [ import                    ]
                             ;

new                         := "new"             [ push: 1                   ]
                               identifier        [ push: 1                   ]
                               func_call         [ new_obj                   ]
                             ;

assignment_or_call          := assignment
                             |                   [ push: 0                   ]
                               func_call         [ emit_pop                  ]
                             ;

assignment                  := =
                               expr              [ emit_assign               ]
                             ;

expr                        := predicate
                               predicatetail
                             ;
                         
predicatetail               := logic_op          [ push: $                   ]
                               predicate         [ emit_infix_op             ]
                             |
                             ;
                               
predicate                   := term
                               termtail
                             ;

termtail                    := add_op            [ push: $                   ]
                               term              [ emit_infix_op             ]
                               termtail
                             | 
                             ;

term                        := factor factortail 
                             ;

factortail                  := mult_op           [ push: $                   ]
                               factor            [ emit_infix_op             ] 
                               factortail
                             | 
                             ;

factor                      := ( expr )
                             | var_or_call 
                             | constant  
                             ;

var_or_call                 := new
                             |                   [ push: 0                   ] 
                               identifier
                               call_or_empty
                             ;
                              
call_or_empty               :=                   [ push: 0                   ] 
                               func_call
                             |                   [ emit_pushvar              ]
                             ;
                             
identifier                  := 'i'               [ push++: $                 ]
			       identifier_tail
			     ;

identifier_tail             := .
                               identifier
                             |
                             ;
                             
constant                    := '"'               [ emit_pushval              ]  
                             | '\''              [ emit_pushval              ]  
                             | signed_number
			     | list
                             ;

signed_number               := sign
                               number            [ push_signed_val           ]
                            ;

sign                        := +                 [ push: $                   ]
                             | -                 [ push: $                   ]
                             |                   [ push: '+'                 ]
                             ;

number                      := 'd'
                             | 'f'
                             | 'x'
                             ;
                               
list                        :=                   [ push: 1 push: "list"      ]
                               '['               [ push: 0                   ]
                               entrylist_or_empty
                               ']'               [ emit_func_call            ]
                             ;

entrylist_or_empty          := entrylist
                             |
                             ;

entrylist                   := expr              [ ++                        ]
                               entrylist_tail
                             ;

entrylist_tail              := ,
			       entrylist
                             |
			     ;

logic_op                    := > | ">=" | < | "<=" | "==" | "!=" ;
                             
add_op                      := + | - ;

mult_op                     := * | / | ^ | % ;

func_call                   := (                 [ push: 0                   ]
                               arglist_or_void
                               )                 [ emit_func_call            ]
                             ;

arglist_or_void             := arglist
                             |
                             ;

arglist                     := argument
                               arglist_tail
                             ;

argument                    := expr              [ ++                        ] 
                             ;

arglist_tail                := ,
                               arglist
                             |
                             ;
 
